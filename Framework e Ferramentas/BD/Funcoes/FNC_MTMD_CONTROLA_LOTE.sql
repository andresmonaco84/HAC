CREATE OR REPLACE FUNCTION FNC_MTMD_CONTROLA_LOTE (
pCAD_MTMD_ID                  IN TB_MTMD_ESTOQUE_LOTE.CAD_MTMD_ID%type,
pMTMD_LOTEST_ID               IN TB_MTMD_LOTEST_LOTE_ESTOQUE.MTMD_LOTEST_ID%type DEFAULT NULL,
pMTMD_FL_CONTROLA_LOTE        OUT NUMBER -- 0 / 1 (NAO / SIM) Verifica se item controla lote conforme seu cadastro
) RETURN  NUMBER IS --Retorna 0 ou 1 (false/true) de acordo com que o lote (pMTMD_LOTEST_ID) tenha controle ou nao
vCAD_MTMD_GRUPO_ID         TB_CAD_MTMD_MAT_MED.cad_mtmd_grupo_id%TYPE;
vCAD_MTMD_FL_CONTROLA_LOTE TB_CAD_MTMD_MAT_MED.CAD_MTMD_FL_CONTROLA_LOTE%TYPE;
vMTMD_CONTROLA_LOTEST      TB_MTMD_LOTEST_LOTE_ESTOQUE.MTMD_CONTROLA_LOTEST%TYPE;
vMTMD_COD_LOTE             TB_MTMD_LOTEST_LOTE_ESTOQUE.MTMD_COD_LOTE%TYPE;
/***********************************************************************************************
*    Function: FNC_MTMD_CONTROLA_LOTE
*
*    Data Criacao:   02/2018         Por: Andre
*
*    Funcao: RETORNA SE ITEM/LOTE TEM CONTROLE DE LOTE
*            APENAS GRUPO DE MEDICAMENTOS TERAO ESTE CONTROLE
*************************************************************************************************/
BEGIN

  SELECT PROD.CAD_MTMD_GRUPO_ID, NVL(PROD.CAD_MTMD_FL_CONTROLA_LOTE,0)
    INTO vCAD_MTMD_GRUPO_ID,     vCAD_MTMD_FL_CONTROLA_LOTE
  FROM TB_CAD_MTMD_MAT_MED PROD
  WHERE PROD.CAD_MTMD_ID = pCAD_MTMD_ID;

  IF (vCAD_MTMD_GRUPO_ID = 1 AND vCAD_MTMD_FL_CONTROLA_LOTE = 1) THEN
    pMTMD_FL_CONTROLA_LOTE := 1;
  ELSE
    pMTMD_FL_CONTROLA_LOTE := 0;
  END IF;

  IF (pMTMD_LOTEST_ID IS NOT NULL) THEN

    BEGIN
      SELECT LOTE.MTMD_COD_LOTE
        INTO vMTMD_COD_LOTE
      FROM TB_MTMD_LOTEST_LOTE_ESTOQUE LOTE
      WHERE LOTE.CAD_MTMD_FILIAL_ID = 1 AND
            LOTE.MTMD_LOTEST_ID = pMTMD_LOTEST_ID AND
            LOTE.CAD_MTMD_ID    = pCAD_MTMD_ID;
    EXCEPTION WHEN NO_DATA_FOUND THEN
       RETURN 0;
    END;

    IF (vMTMD_COD_LOTE IS NOT NULL) THEN
      vMTMD_CONTROLA_LOTEST := FNC_MTMD_CONTROLA_LOTE_COD(pCAD_MTMD_ID, vMTMD_COD_LOTE);
    END IF;

    IF (vCAD_MTMD_GRUPO_ID = 1 AND vCAD_MTMD_FL_CONTROLA_LOTE = 1 AND vMTMD_CONTROLA_LOTEST = 1) THEN
      RETURN 1;
    END IF;
  END IF;

  RETURN 0;
END FNC_MTMD_CONTROLA_LOTE;