CREATE OR REPLACE PROCEDURE SGS.PRC_MTMD_PEDIDO_PADRAO_IMP_S
  (     
     pCAD_LAT_ID_LOCAL_ATENDIMENTO IN TB_MTMD_PEDIDO_PADRAO.CAD_LAT_ID_LOCAL_ATENDIMENTO%type DEFAULT NULL,
     pCAD_UNI_ID_UNIDADE IN TB_MTMD_PEDIDO_PADRAO.CAD_UNI_ID_UNIDADE%type DEFAULT NULL,
     pCAD_SET_ID IN TB_MTMD_PEDIDO_PADRAO.CAD_SET_ID%type DEFAULT NULL,
     pCAD_MTMD_FILIAL_ID IN TB_MTMD_PEDIDO_PADRAO.CAD_MTMD_FILIAL_ID%type DEFAULT NULL,
     io_cursor OUT PKG_CURSOR.t_cursor
  )
  is
  /********************************************************************
  *    Procedure: PRC_MTMD_PEDIDO_PADRAO_S
  *
  *    Data Criacao: 	21/06/2010  Por: RICARDO COSTA
  *    Data Alteracao:	29/09/2010  Por: RICARDO COSTA
  *         Alterac?o:  ATULIZADA MIGRA2
  *    Data Alteracao:	26/02/2013  Por: Andre
  *         Alterac?o:  Quando n?o havia Data Dispensac?o n?o estava trazendo os registros
  *    Data Alteracao:	20/07/2018  Por: Andre
  *         Alterac?o:  Trazer também de acordo com o novo campo que indica setor como farmácia
  *
  *    Funcao: LISTA PEDIDOS PADR?ES PARA GERAR REQUISIC?O
  *       OBS: A COLUNA STATUS ESTA SENDO UTILIZADA PARA VERIFICA SE EXISTE ALGUMA REQUISICAO 
  *            DOS TIPOS 3=FECHADA ( ENVIADA AO ALMOXARIFADO) 5=IMPRESSA ( MAT EMITIDO  )  
  *            OU NULL QUANDO N?O EXISTE NENHUMA
  *
  *******************************************************************/
  v_cursor PKG_CURSOR.t_cursor;
   vUNIDADE_ESTOQUE_CONSUMO   TB_MTMD_MOV_MOVIMENTACAO.CAD_UNI_ID_UNIDADE%type;
   vLOCAL_ESTOQUE_CONSUMO     TB_MTMD_MOV_MOVIMENTACAO.CAD_LAT_ID_LOCAL_ATENDIMENTO%type;
   vSETOR_ESTOQUE_CONSUMO     TB_MTMD_MOV_MOVIMENTACAO.CAD_SET_ID%type;
   vCAD_SET_ALMOX_CENTRAL     TB_CAD_SET_SETOR.CAD_SET_ALMOX_CENTRAL%TYPE;
   vCAD_SET_FL_SUBSTALMOX_OK  TB_CAD_SET_SETOR.CAD_SET_FL_SUBSTALMOX_OK%TYPE;
   AlmoxCentral               NUMBER;
   Farmacia                   NUMBER;
  BEGIN  
    -- VERIFICA SE E ALMOX CENTRAL OU CENTRO DE DISPENSACAO
    BEGIN
       SELECT SETOR.CAD_SET_ALMOX_CENTRAL,   SETOR.CAD_SET_FL_SUBSTALMOX_OK
       INTO   vCAD_SET_ALMOX_CENTRAL,        vCAD_SET_FL_SUBSTALMOX_OK
       FROM TB_CAD_SET_SETOR SETOR
       WHERE SETOR.CAD_UNI_ID_UNIDADE           = pCAD_UNI_ID_UNIDADE
       AND   SETOR.CAD_LAT_ID_LOCAL_ATENDIMENTO = pCAD_LAT_ID_LOCAL_ATENDIMENTO
       AND   SETOR.CAD_SET_ID                   = pCAD_SET_ID;       
    END;
    IF (( vCAD_SET_ALMOX_CENTRAL = 1 ) OR ( vCAD_SET_FL_SUBSTALMOX_OK = 'S'  )) THEN    
       PRC_MTMD_ESTOQUE_DE_CONSUMO( pCAD_UNI_ID_UNIDADE,
                                    pCAD_LAT_ID_LOCAL_ATENDIMENTO,
                                    pCAD_SET_ID, 
                                    pCAD_MTMD_FILIAL_ID,
                                    vUNIDADE_ESTOQUE_CONSUMO,
                                    vLOCAL_ESTOQUE_CONSUMO,
                                    vSETOR_ESTOQUE_CONSUMO                                
                                  );
       AlmoxCentral := 1; -- por padrao assume que e o central
       -- VERIFICO SE E CENTRAL OU CENTRO DE DISPENSACAO
       BEGIN
          SELECT 0
          INTO AlmoxCentral
          FROM TB_CAD_SET_SETOR SETOR
          WHERE SETOR.CAD_SET_SETOR_ALMOX    = vSETOR_ESTOQUE_CONSUMO
          AND   SETOR.CAD_SET_LOCAL_ALMOX    = vLOCAL_ESTOQUE_CONSUMO
          AND   SETOR.CAD_SET_UNIDADE_ALMOX  = vUNIDADE_ESTOQUE_CONSUMO;
       EXCEPTION 
          WHEN NO_DATA_FOUND THEN
             AlmoxCentral := 1;
          WHEN TOO_MANY_ROWS THEN
             -- RETORNOU VARIOS N?O E CENTRAL
             AlmoxCentral := 0;      
       END;
    ELSE 
       -- NAO E ALMOX CENTRAL OU CENTRO DISP.
       AlmoxCentral :=0;       
       --Verifica se e farmacia
       BEGIN
          SELECT 1
            INTO Farmacia
            FROM TB_CAD_SET_SETOR SETOR
           WHERE SETOR.CAD_SET_SETOR_FARMACIA = pCAD_SET_ID 
             AND ROWNUM = 1;
       EXCEPTION 
          WHEN NO_DATA_FOUND THEN
             Farmacia := 0;
       END;
    END IF;
    IF (NVL(Farmacia,0) = 0) THEN
      OPEN v_cursor FOR
      SELECT
         PEDIDO.MTMD_PEDPAD_ID,
         PEDIDO.CAD_LAT_ID_LOCAL_ATENDIMENTO,
         PEDIDO.CAD_UNI_ID_UNIDADE,
         PEDIDO.CAD_SET_ID,
         PEDIDO.CAD_MTMD_FILIAL_ID,
         PEDIDO.MTMD_PERIODO_DIAS,
         PEDIDO.MTMD_DT_DISPENSACAO,
         PEDIDO.MTMD_DT_ULT_REQUISICAO,
         SETOR.CAD_SET_DS_SETOR,
         UNIDADE.CAD_UNI_DS_UNIDADE,
         LOC.CAD_LAT_DS_LOCAL_ATENDIMENTO,
         (  SELECT REQUISICAO.MTMD_REQ_FL_STATUS
                 FROM TB_MTMD_REQ_REQUISICAO       REQUISICAO
                 WHERE REQUISICAO.CAD_UNI_ID_UNIDADE           = PEDIDO.CAD_UNI_ID_UNIDADE
                 AND   REQUISICAO.CAD_LAT_ID_LOCAL_ATENDIMENTO = PEDIDO.CAD_LAT_ID_LOCAL_ATENDIMENTO
                 AND   REQUISICAO.CAD_SET_ID                   = PEDIDO.CAD_SET_ID
                 AND   REQUISICAO.MTMD_REQ_FL_STATUS           IN ( PKG_MTMD_CONSTANTES.REQ_STATUS_FECHADA, PKG_MTMD_CONSTANTES.REQ_STATUS_IMPRESSA )
                 AND   REQUISICAO.CAD_MTMD_FILIAL_ID           = PEDIDO.CAD_MTMD_FILIAL_ID
                 AND   REQUISICAO.MTM_TIPO_REQUISICAO          = PKG_MTMD_CONSTANTES.REQ_PADRAO
                 AND   ROWNUM =1 
         ) MTMD_FL_STATUS
      FROM TB_MTMD_PEDIDO_PADRAO        PEDIDO,
           TB_CAD_SET_SETOR             SETOR,
           TB_CAD_UNI_UNIDADE           UNIDADE,
           TB_CAD_LAT_LOCAL_ATENDIMENTO LOC
     -- ALMOXARIFADO CENTRAL / BUSCO TODAS AS REQUISIC?ES DOS SETORES SEM CENTRO DE DISPENSAC?O         
      WHERE ( AlmoxCentral = 0 OR ( SETOR.cad_set_local_almox       IS NULL
                                    AND SETOR.cad_set_unidade_almox IS NULL
                                    AND SETOR.cad_set_setor_almox   IS NULL ) )
      -- CENTRO DE DISPENSACAO / BUSCO TODAS AS REQUISIC?ES DOS SETORES QUE S?O DESTE CENTRO DE DISPENSAC?O
      AND   ( AlmoxCentral = 1 OR (     SETOR.cad_set_local_almox   = vLOCAL_ESTOQUE_CONSUMO
                                    AND SETOR.cad_set_unidade_almox = vUNIDADE_ESTOQUE_CONSUMO
                                    AND SETOR.cad_set_setor_almox   = vSETOR_ESTOQUE_CONSUMO) )
      AND   (pCAD_MTMD_FILIAL_ID is null           OR ( PEDIDO.CAD_MTMD_FILIAL_ID         = pCAD_MTMD_FILIAL_ID ))
   -- AND   (pMTMD_DT_ULT_REQUISICAO IS NULL       OR (NVL(PEDIDO.MTMD_DT_ULT_REQUISICAO, SYSDATE) - PEDIDO.MTMD_PERIODO_DIAS) + PEDIDO.MTMD_PERIODO_DIAS) <= TRUNC(SYSDATE))
   -- AND   NVL(PEDIDO.MTMD_DT_DISPENSACAO, TRUNC(SYSDATE)) <= TRUNC(SYSDATE) - PEDIDO.MTMD_PERIODO_DIAS 
   -- AND   NVL(PEDIDO.MTMD_DT_DISPENSACAO, TRUNC(SYSDATE)) between (TRUNC(SYSDATE) - PEDIDO.MTMD_PERIODO_DIAS) and SYSDATE
      AND   NVL(PEDIDO.MTMD_DT_DISPENSACAO, TRUNC(SYSDATE-30)) <= TO_DATE(TO_CHAR(TRUNC(sysdate)- PEDIDO.MTMD_PERIODO_DIAS,'DDMMYYYY')||' 2359', 'DDMMYYYY HH24MI')
      AND   SETOR.CAD_SET_ID                      = PEDIDO.CAD_SET_ID
      AND   UNIDADE.CAD_UNI_ID_UNIDADE            = PEDIDO.CAD_UNI_ID_UNIDADE
      AND   LOC.CAD_LAT_ID_LOCAL_ATENDIMENTO      = PEDIDO.CAD_LAT_ID_LOCAL_ATENDIMENTO
      ORDER BY UNIDADE.CAD_UNI_DS_UNIDADE, LOC.CAD_LAT_DS_LOCAL_ATENDIMENTO, SETOR.CAD_SET_DS_SETOR;
      io_cursor := v_cursor;
   ELSE
     --TRAZER APENAS SETORES ABASTECIDOS PELA FARMACIA
     OPEN v_cursor FOR
     SELECT
         PEDIDO.MTMD_PEDPAD_ID,
         PEDIDO.CAD_LAT_ID_LOCAL_ATENDIMENTO,
         PEDIDO.CAD_UNI_ID_UNIDADE,
         PEDIDO.CAD_SET_ID,
         PEDIDO.CAD_MTMD_FILIAL_ID,
         PEDIDO.MTMD_PERIODO_DIAS,
         PEDIDO.MTMD_DT_DISPENSACAO,
         PEDIDO.MTMD_DT_ULT_REQUISICAO,
         SETOR.CAD_SET_DS_SETOR,
         UNIDADE.CAD_UNI_DS_UNIDADE,
         LOC.CAD_LAT_DS_LOCAL_ATENDIMENTO,
         (  SELECT REQUISICAO.MTMD_REQ_FL_STATUS
                 FROM TB_MTMD_REQ_REQUISICAO       REQUISICAO
                 WHERE REQUISICAO.CAD_UNI_ID_UNIDADE           = PEDIDO.CAD_UNI_ID_UNIDADE
                 AND   REQUISICAO.CAD_LAT_ID_LOCAL_ATENDIMENTO = PEDIDO.CAD_LAT_ID_LOCAL_ATENDIMENTO
                 AND   REQUISICAO.CAD_SET_ID                   = PEDIDO.CAD_SET_ID
                 AND   REQUISICAO.MTMD_REQ_FL_STATUS           IN ( PKG_MTMD_CONSTANTES.REQ_STATUS_FECHADA, PKG_MTMD_CONSTANTES.REQ_STATUS_IMPRESSA )
                 AND   REQUISICAO.CAD_MTMD_FILIAL_ID           = PEDIDO.CAD_MTMD_FILIAL_ID
                 AND   REQUISICAO.MTM_TIPO_REQUISICAO          = PKG_MTMD_CONSTANTES.REQ_PADRAO
                 AND   ROWNUM =1 
         ) MTMD_FL_STATUS
      FROM TB_MTMD_PEDIDO_PADRAO        PEDIDO,
           TB_CAD_SET_SETOR             SETOR,
           TB_CAD_UNI_UNIDADE           UNIDADE,
           TB_CAD_LAT_LOCAL_ATENDIMENTO LOC
      WHERE SETOR.CAD_SET_SETOR_FARMACIA = pCAD_SET_ID
      AND   (pCAD_MTMD_FILIAL_ID is null OR ( PEDIDO.CAD_MTMD_FILIAL_ID = pCAD_MTMD_FILIAL_ID))
      AND   NVL(PEDIDO.MTMD_DT_DISPENSACAO, TRUNC(SYSDATE-30)) <= TO_DATE(TO_CHAR(TRUNC(sysdate)- PEDIDO.MTMD_PERIODO_DIAS,'DDMMYYYY')||' 2359', 'DDMMYYYY HH24MI')
      AND   SETOR.CAD_SET_ID                      = PEDIDO.CAD_SET_ID
      AND   UNIDADE.CAD_UNI_ID_UNIDADE            = PEDIDO.CAD_UNI_ID_UNIDADE
      AND   LOC.CAD_LAT_ID_LOCAL_ATENDIMENTO      = PEDIDO.CAD_LAT_ID_LOCAL_ATENDIMENTO
      ORDER BY UNIDADE.CAD_UNI_DS_UNIDADE, LOC.CAD_LAT_DS_LOCAL_ATENDIMENTO, SETOR.CAD_SET_DS_SETOR;
     io_cursor := v_cursor;
   END IF;
  end PRC_MTMD_PEDIDO_PADRAO_IMP_S;